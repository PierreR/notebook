= Nix

== NixOS

=== icon:tasks[] Install

. switch to azerty keyboard
+
```
→ loadkeys be-latin1
```
. partition with gdisk (efi) or fdisk (noefi)
+
icon:info-circle[1x] using virtualbox you don't want/need efi
+
```
→ (g/f)disk /dev/sda
```
+
create 2 partitions sda1(83 default)/sda2(82)
. create file system
+
```
→ mkfs.extmre4 -L nixos /dev/sda1
→ mkswap -L swap /dev/sda2
```
. mount it
+
```
→ mount /dev/disk/by-label/nixos /mnt
```
. generate a default config
+
`→ nixos-generate-config --root /mnt`
. minimal edit the config; don't forget to uncomment the option 'boot.loader.grub.device'
+
```
→ nano /mnt/etc/nixos/configuration.nix
```
. Install
+
`→ nixos-install
. reboot
+
```
→ reboot
```
. upgrade
+
```
→ nixos-rebuild boot --upgrade
→ reboot
```
. <<Configuration>>

[[Configuration]]
=== icon:wrench[] Configuration

[NOTE]
====
Some nix properties are set up in `/etc/nix/nix.conf`

for `wifi`, manually configure it by using `NetworkManager` through the `nmtui` text interface
====

./etc/nixos/configuration
```nix
  nixpkgs.config.allowUnfree = true;

  i18n = {
    consoleFont = "Lat2-Terminus16";
    consoleKeyMap = "be-latin1";
    defaultLocale = "en_US.UTF-8";
  } ;

  environment.systemPackages = with pkgs; [
    asciidoctor <1>
  ];

  # Define a user account. Don't forget to set a password with ‘passwd’.
  users.extraUsers.nix = { <2>
    createHome = true;
    home = "/home/nix";
    isSystemUser = true;
    extraGroups = [ "wheel" "disk" "vboxusers" "docker"];
    shell = "/run/current-system/sw/bin/bash";
    uid = 1000;
  };

  programs.bash.enableCompletion = true;
  security.sudo.wheelNeedsPassword = false;

  fonts = {
    enableFontDir = true;
    fonts = [ pkgs.source-code-pro ];
  };

  nix.extraOptions = ''
    gc-keep-outputs = true <3>
    gc-keep-derivations = true <3>
  '';

  virtualisation.docker.enable = true;
  virtualisation.docker.extraOptions = "--insecure-registry x.lan --insecure-registry y.lan";

  virtualisation.virtualbox.guest.enable = true; <4>
  boot.initrd.checkJournalingFS = false; <4>

```
<1> add packages
<2> do create a new user !  +
(root won't be able to have a chromium session by default)
<3> prevent too much gc in developer environment
<4> virtualbox only

=== icon:cog[] System management

.Update
```
→ sudo nixos-rebuild switch
→ sudo nixos-rebuild boot --upgrade <1>
```
<1> safer to use `boot` when upgrading

== Derivation

In nix, packages are called derivations. Derivations are stored in the nix store as follows: /nix/store/hash-name, where the hash uniquely identifies the derivation (not true, it's a little more complex than this), and name is the name of the derivation.

From a nix language point of view, a derivation is simply a set, with some attributes.

To build a package, `nixpkgs` makes heavy usage of `stdenv` and its function `mkDerivation`:

```nix
stdenv.mkDerivation {
  name = "libfoo-${version}"; <1>
  version = "1.2.3"
  src = fetchurl {
    url = http://example.org/libfoo-1.2.3.tar.bz2;
    md5 = "e1ec107956b6ddcb0b8b0679367e9ac9"; <2>
  };
  builder = ./builder.sh; <3>
  buildInputs = [ruby]; <4>
}
```
<1> mandatory `name` attr
<2> mandatory checksum for remote source
<3> if not provided, the generic builder is used
<4> additional requirement needed to build the derivation

The output of a derivation needs to be deterministic. That's why you can fetch source remotely iff you know the hash beforehand.

====
drv files:: specification of how to build a derivation similar to the `.o` file in C.
====

== Channels

A channel is the Nix mechanism for distributing a consistent set of Nix expressions and binaries.
nix-channel --add

```
→ nix-channel --add http://nixos.org/channels/nixpkgs-unstable
→ nix-channel --update
→ nixos-rebuild switch
```

The unstable channel is usually a few days older from `nixpkgs` master.
For a precise status, check http://howoldis.herokuapp.com[here].

You can directly use a derivation from master. For instance, after cloning `nixpkgs`, you could type:

```
→  NIX_PATH=nixpkgs=/home/vagrant/projects/nix/nixpkgs nix-env -f '<nixpkgs>' -iA haskellPackages.stack
```

NOTE: On `nixos`, you should stick to `nixos-unstable` (don't use `nixpkgs-unstable` because specific nixos sanity check won't applied)

== Nix-shell

When Nix builds a package, it builds it in an isolated environment. It does this by creating a clean, child shell, then adding only the dependencies the package declares. After setting up the dependencies, it runs the build script, moves the built app into the Nix store, and sets up the environment to point to it. Finally, it destroys this child shell.

But we can ask Nix to not destroy the child shell, and instead let us use it for working iteratively on the app. This is what the `nix-shell` is about:

```
 nix-shell '<nixpkgs>' -p ruby haskellPackages.stack <1>
```
<1> `p` and `-A` are mutually exclusive

If a path is not given, nix-shell defaults to `shell.nix` if it exists, and `default.nix` otherwise. This allows for a nice trick. We can decribe a virtual dev environment (of any sort for any language) by decribing a derivation in `default.nix` like so:

.default.nix
```nix
with import <nixpkgs> {};

let henv = haskellPackages.ghcWithPackages (p: with p; [shake]);

in
stdenv.mkDerivation {
  name = "haskell-env";
  buildInputs = [ henv pythonPackages.pyyaml];
}
```
[NOTE]
====
In Haskell, we can use https://github.com/NixOS/nixpkgs/blob/b40e1efe000ec5c4616cecc9d6836eade419434e/pkgs/development/haskell-modules/generic-builder.nix#L283[env] to build the isolated environment:

.shell.nix
```nix
with (import <nixpkgs> {}).pkgs;
(haskellPackages.callPackage ./. {}).env <1>
```
<1> `callPackage` will use the current defined scope to pass matched arguments

`default.nix` is then generated by `cabal2nix` to describe how to nix-build the haskell package.
====

You can force any script file to run in a nix-shell as such:
```
#! /usr/bin/env nix-shell
#! nix-shell -i bash
```
or without a default.nix file:

```
#! /usr/bin/env nix-shell
#! nix-shell -i bash -p pythonPackages.pyyaml -p '(import <nixpkgs> {}).haskellPackages.ghcWithPackages (p: with p; [shake])'
```
== Commands

==== nix-env

`nix-env` is the command to use to search, install, remove packages locally in user space (or profile). These packages are installed in the `nix-store` but are only accessible inside one environment (aka user/profile).

[NOTE]
====
`nix-env` doesn't require a starting nix expression. As a consequence, `nix-env` does not use `<nixpkgs>` as NIX_PATH. It actually uses `~/.nix-defexpr/channels`.
If you want to use <nixpkgs>, you would explicitly use the `-f` flag on the command line.
====

- *-q* list installed derivations within a profile
- *-qaP* list available package with the path
+
[WARNING]
====
The Haskell package set is not registered in the top-level namespace because it is huge. As a consequence, when searching for haskell  packages you need to provide the `haskellPackages` attribute:
```
→ nix-env -f '<nixpkgs>' -qaP -A haskellPackages.shake <1>
# In nixos, you can also do:
→ nix-env -qaP -A nixos.pythonPackages
```
<1> together with `-A`, `-f` needs to be provided.

====
- *-i* install derivations
+
```
→ nix-env -f '<nixpkgs>' -iA pythonPackages.pyyaml
→ nix-env -e python2.7-PyYAML-3.11
→ nix-env -f '<nixpkgs>' -i brackets -I nixpkgs=https://github.com/NixOS/nixpkgs/archive/master.tar.gz’ <1>
```
<1> install from master directly

- *-u* update
+
```
→ nix-env -u '*'
```

==== nix-build

nix-build tool does two main jobs:

* nix-instantiate: parse the `.nix` file and return the .drv file
* nix-store -r: realise the .drv, which actually builds the derivation

```
```

== Language Expressions

String::
+
```nix
let
  h = "Hello";
  value = 4;
in
{
  helloWorld = "${h} ${toString value} the win!"; <1>
}
```
<1> interpolation of the `toString` builtin function to convert an int value

List::
+
```nix
[ 123 ./foo.nix "abc" (f { x = y; }) ]
```

Set::

+
```nix
let x = 12;
    y = 34;
    f = {n}: 5 + n;
in
rec {
  r = { inherit x y; <1>
    text = "Hello";
    add = f { n = 56; }; <2>
  };
  sum = r.add + r.y;
  hello = r.text or "World"; <3>
  b = r ? x; <4>
}
```
<1> when defining a set it is often convenient to copy variables from the surrounding lexical scope
<2> all `;` are mandatory
<3> Sets accessor using `.` +
    Default using `or`
<4> does the record 'r' contains an attribute 'x' `?`

Function::
+
```nix
pattern: body
```
+
```nix
# `min` and `max` are available in stdenv.lib
min = x: y: if x < y then x else y; <1>
```
<1> pattern is a func returning a func (2 arguments)
+
```nix
{stdenv, fetchurl, perl, ... }: <1>

  stdenv.mkDerivation { <2>
    name = "hello-2.1.1";
	...
  };
```
<1> pattern is a set of arguments +
the 'ellipsis' (`...`) allows the passing of a bigger set, one that contains more than the 3 required arguments.
<2> function call passing a set as argument

With::
+
```nix
with e1; e2
```
Introduces all attributes of the set `e1` into the lexical scope of the expression `e2`:
+
```nix
let as = { x = "foo"; y = "bar"; };
in
foobar = with as; x + y
```

Optional argument::
+
```nix
{ x, y ? "foo", z ? "bar" }: z + y + x <1>
```
<1> a function that only requires an attribute named x, but optionally accepts y and z.

Merge sets::
+
```nix
e1 // e2 # merge e1 and e2 with e2 taking precedence in case of equally named attribute
```

== Idioms

Override pkgs derivations::
+
[source, nix, highlight="2"]
----
pkgs.overridePackages (self: super: {
  foo = super.foo.override { barSupport = true ; }; <1>
})
----
<1> you may call #override# on much derivations to changes attribute value(s).
+
Instead of using #overridePackages# in `~/.nixpkgs/config.nix` you would typically declare a #packageOverrides# function knowing it will be called to customize '<nixpkgs>' locally:
+
.~/.nixpkgs/config.nix
[source, nix, highlight="2"]
----
{
  packageOverrides = super:
    let self = super.pkgs;
    in
    {
      haskellPackages = super.haskellPackages.override {
        overrides = self: super: { <1>
        ...
        };
    };
}
----
<1> override the #overrides# attribute of haskellPackages

Private packages::
+
You can easily extend `nixpkgs` with private derivations without any forking by writing a custom file:
+
.dotfiles.nix
```nix
with import <nixpkgs> {}; <1>

let xmonadEnv = haskellPackages.ghcWithPackages (p: with p; [xmonad xmonad-contrib]); <2>
in

stdenv.mkDerivation {
  name = "devbox_dotfiles-0.1";

  src = fetchFromGitHub {
    owner = "CIRB";
    repo = "devbox-dotfiles";
    rev = "801f66f3c7d657f5648963c60e89743d85133b1a" ;
    sha256 = "1w4vaqp21dmdd1m5akmzq4c3alabyn0mp94s6lqzzp1qpla0sdx0" ;
  };

  buildInputs = [ xmonadEnv ];

  installPhase = ''
    ${xmonadEnv}/bin/ghc --make .xmonad/xmonad.hs -o .xmonad/xmonad-x86_64-linux <3>
    cp -R ./. $out <4>
  '';

  meta = {
    description = "Dot files for the devbox";
  };
}
```
<1> dependencies provided by `nixpkgs` using $NIX_PATH
<2> ghc with module deps included
<3> at this stage, the shell is inside a temp dir with the src included
<4> copy the content of the current dir into $out
+
You then build the derivation or install it in the user environment.
+
```
→ nix-build dotfiles.nix
→ nix-env -f dotfiles.nix -i devbox_dotfiles <1>
```
<1> `nix-env -i` takes the name attribute and strip the version (first numeric after `-`)

Override pattern::
+
```nix
fix = f:
  let self = f self;
  in self;

extend = attrs: f: self:
  let super = attrs self;
  in super // f self super;

ps = self:
  { foo = "foo"; bar = "bar";
     foobar = self.foo + self.bar;
  };

f = self: super:
  { foo = reverse super.foo; }

(fix ps).foobar # "foobar"

(fix (extend ps f)).foobar # "oofbar"
```

Pinned a version of nixpkgs::
+
```
  nixpkgs = (import <nixpkgs> {}).pkgs.fetchgit {
              url = "https://github.com/nixos/nixpkgs-channels";
              rev = "f93a8ee1105f4cc3770ce339a8c1a4acea3b2fb6";
              sha256 = "01fnyw711p6kf9qpdabys9im10hlih1l1pxwp06wkq7b9wsljawd";
            };

in

with import nixpkgs {};
```

== Bootstrap

Nix composes all of these individual functions into a large package repository. This repository essentially calls every single top level function, with support for recursive bindings in order to satisfy dependencies. Continuing with the hello example, we may have a top-level entry point like:


```nix
rec {
  hello = import /path/to/hello.nix { inherit stdenv fetchurl; }; <1>

  stdenv = import /path/to/stdenv.nix { inherit gcc };

  fetchurl = import /path/to ;

  gcc = import /path/to/gcc.nix {};

  # ...
}
```
<1> Import loads a file containing a function and then calls that function with the provided arguments

> But wait - I just said this calls all functions… so wouldn’t that then mean that all software gets installed? The trick here is that Nix is a lazy language.


== Ruby

* Create or copy a Gemfile at the root dir of the project
* Install bundler in my user profile if it is not already there.
* Create a Gemfile.lock by running bundler lock
* Use bundix in the target directory: $(nix-build '<nixpkgs>' -A bundix)/bin/bundix. It will create a gimset.nix file
* Create a default.nix file

== References

- http://lethalman.blogspot.be/search/label/nixpills[nix pills]
- http://sandervanderburg.blogspot.be/2014/07/managing-private-nix-packages-outside.html[private packages]
- https://nixcloud.io/tour/?id=1
